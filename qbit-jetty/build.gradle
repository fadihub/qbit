/*
 * Copyright (c) 2015. Rick Hightower, Geoff Chandler
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *  		http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * QBit - The Microservice lib for Java : JSON, WebSocket, REST. Be The Web!
 */

sourceSets.main.resources.srcDir 'src/main/java'
apply plugin: 'signing'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    required false
    sign configurations.archives
}

jar {
    manifest {
        attributes 'Main-Class': 'io.rd.cognizance.example.SampleService'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'

    compile 'org.eclipse.jetty:jetty-server:9.3.0.M1'
    compile 'org.eclipse.jetty:jetty-servlet:9.3.0.M1'
    compile 'org.eclipse.jetty.websocket:javax-websocket-server-impl:9.3.0.M1'
    compile 'org.eclipse.jetty:jetty-annotations:9.3.0.M1'
    compile 'org.eclipse.jetty:jetty-client:9.3.0.M1'
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'javax.websocket:javax.websocket-all:1.1'

    compile project(':qbit-servlet')
    compile project(':qbit-core')
    compile project(':qbit-boon')

    testCompile project(':qbit-test')

}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                try {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                } catch (MissingPropertyException ignore) {
                }
            }

            pom.project {
                name 'qbit-vertx'
                packaging 'jar'
                description 'Go Channels inspired Java lib'
                url 'https://github.com/advantageous/qbit'

                scm {
                    url 'scm:git@github.com:advantageous/qbit.git'
                    connection 'scm:git@github.com:advantageous/qbit.git'
                    developerConnection 'scm:git@github.com:advantageous/qbit.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'richardHightower'
                        name 'Richard Hightower'
                    }
                    developer {
                        id 'sailorgeoffrey'
                        name 'Geoffrey Chandler'
                    }
                }
            }
        }
    }
}
