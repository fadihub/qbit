/*
 * Copyright (c) 2015. Rick Hightower, Geoff Chandler
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *  		http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * QBit - The Microservice lib for Java : JSON, WebSocket, REST. Be The Web!
 */

allprojects {

    group = 'io.advantageous.qbit'
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'application'

    version = '0.6.1-SNAPSHOT'

    test.onlyIf { !Boolean.getBoolean('skip.tests') }
}

subprojects {

    repositories {
        mavenLocal()
        mavenCentral()
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8


    task buildDockerfile(type: Dockerfile) {
        dependsOn distTar
        from "java:openjdk-8"
        add "$distTar.archivePath", "/"
        workdir "/$distTar.archivePath.name" - ".$distTar.extension" + "/bin"
        entrypoint "./$project.name"
    }

    task buildDockerImage(type: Exec) {
        dependsOn buildDockerfile
        commandLine "docker", "build", "-t", "$project.group/$project.name:$version", buildDockerfile.dockerDir
    }


    task runDockerImage(type: Exec) {
        dependsOn buildDockerImage
        commandLine "docker", "run", "-t", "$project.group/$project.name:$version", buildDockerfile.dockerDir
    }


    task dockerRun(type: Exec) {
        commandLine "docker", "run", '-m="4g"', "-t", "$project.group/$project.name:$version", buildDockerfile.dockerDir
    }
}

project(':qbit-core') {

    apply plugin: 'signing'

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.10'
        compile group: 'io.fastjson', name: 'boon', version: boonVersion
        compile "org.slf4j:slf4j-api:[1.7,1.8)"
        testCompile 'ch.qos.logback:logback-classic:1.1.2'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    signing {
        required false
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    try {
                        authentication(userName: sonatypeUsername, password: sonatypePassword)
                    } catch (MissingPropertyException ignore) {
                    }
                }

                pom.project {
                    name 'qbit-core'
                    packaging 'jar'
                    description 'Go Channels inspired Java lib'
                    url 'https://github.com/advantageous/qbit'

                    scm {
                        url 'scm:git@github.com:advantageous/qbit.git'
                        connection 'scm:git@github.com:advantageous/qbit.git'
                        developerConnection 'scm:git@github.com:advantageous/qbit.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'richardHightower'
                            name 'Richard Hightower'
                        }
                        developer {
                            id 'sailorgeoffrey'
                            name 'Geoffrey Chandler'
                        }
                    }
                }
            }
        }
    }
}

project(':qbit-boon') {

    apply plugin: 'signing'

    sourceSets.main.resources.srcDir 'src/main/java'
    dependencies {
        compile project(':qbit-core')
        compile group: 'io.fastjson', name: 'boon', version: boonVersion
        compile "org.slf4j:slf4j-api:[1.7,1.8)"
        testCompile 'ch.qos.logback:logback-classic:1.1.2'
        testCompile group: 'junit', name: 'junit', version: '4.10'
        testCompile project(':qbit-test')

    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    signing {
        required false
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    try {
                        authentication(userName: sonatypeUsername, password: sonatypePassword)
                    } catch (MissingPropertyException ignore) {
                    }
                }

                pom.project {
                    name 'qbit-boon'
                    packaging 'jar'
                    description 'Go Channels inspired Java lib'
                    url 'https://github.com/advantageous/qbit'

                    scm {
                        url 'scm:git@github.com:advantageous/qbit.git'
                        connection 'scm:git@github.com:advantageous/qbit.git'
                        developerConnection 'scm:git@github.com:advantageous/qbit.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'richardHightower'
                            name 'Richard Hightower'
                        }
                        developer {
                            id 'sailorgeoffrey'
                            name 'Geoffrey Chandler'
                        }
                    }
                }
            }
        }
    }
}


project(':qbit-vertx') {

    sourceSets.main.resources.srcDir 'src/main/java'
    apply plugin: 'signing'

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    signing {
        required false
        sign configurations.archives
    }

    jar {
        manifest {
            attributes 'Main-Class': 'io.rd.cognizance.example.SampleService'
        }
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    }
    dependencies {
        compile project(':qbit-core')
        compile project(':qbit-boon')
        compile group: 'io.vertx', name: 'vertx-core', version: vertxVersion
        compile group: 'io.vertx', name: 'vertx-platform', version: vertxVersion
        compile "org.slf4j:slf4j-api:[1.7,1.8)"
        testCompile 'ch.qos.logback:logback-classic:1.1.2'
        testCompile group: 'junit', name: 'junit', version: '4.10'
        testCompile project(':qbit-test')


    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    try {
                        authentication(userName: sonatypeUsername, password: sonatypePassword)
                    } catch (MissingPropertyException ignore) {
                    }
                }

                pom.project {
                    name 'qbit-vertx'
                    packaging 'jar'
                    description 'Go Channels inspired Java lib'
                    url 'https://github.com/advantageous/qbit'

                    scm {
                        url 'scm:git@github.com:advantageous/qbit.git'
                        connection 'scm:git@github.com:advantageous/qbit.git'
                        developerConnection 'scm:git@github.com:advantageous/qbit.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'richardHightower'
                            name 'Richard Hightower'
                        }
                        developer {
                            id 'sailorgeoffrey'
                            name 'Geoffrey Chandler'
                        }
                    }
                }
            }
        }
    }
}

project(':qbit-examples-standalone') {
    sourceSets.main.resources.srcDir 'src/main/java'
    dependencies {
        compile project(':qbit-vertx')
        compile "org.slf4j:slf4j-api:[1.7,1.8)"
        compile 'ch.qos.logback:logback-classic:1.1.2'

        testCompile group: 'junit', name: 'junit', version: '4.10'
    }

    task runRestServer(type: JavaExec, dependsOn: 'classes') {

        main = 'io.advantageous.qbit.example.servers.SimpleRestServer'
        classpath = sourceSets.main.runtimeClasspath
    }


    task runRestClient(type: JavaExec, dependsOn: 'classes') {
        main = 'io.advantageous.qbit.example.servers.SimpleRestClient'
        classpath = sourceSets.main.runtimeClasspath
    }

    task runWebSocketClient(type: JavaExec, dependsOn: 'classes') {
        main = 'io.advantageous.qbit.example.servers.SimpleWebSocketClient'
        classpath = sourceSets.main.runtimeClasspath
    }

}

project(':qbit-perf-client') {
    sourceSets.main.resources.srcDir 'src/main/java'
    apply plugin: 'application'

    /*
    You can run this by doing this: gradle run -PappArgs="['localhost', 8080]"
     */

    mainClassName = "io.advantageous.qbit.vertx.http.PerfClientTest"

    run {
        if (project.hasProperty("appArgs")) {
            args Eval.me(appArgs)
        }
    }

    dependencies {
        compile project(':qbit-vertx')
        compile project(':qbit-boon')
        compile group: 'io.fastjson', name: 'boon', version: boonVersion
        compile "org.slf4j:slf4j-api:[1.7,1.8)"
        compile 'ch.qos.logback:logback-classic:1.1.2'

        testCompile group: 'junit', name: 'junit', version: '4.10'
    }

}


project(':qbit-perf-server') {
    sourceSets.main.resources.srcDir 'src/main/java'
    apply plugin: 'application'

    /*
    You can run this by doing this: gradle run -PappArgs="['localhost', 8080]"
     */

    mainClassName = "io.advantageous.qbit.vertx.http.PerfClientTest"

    run {
        if (project.hasProperty("appArgs")) {
            args Eval.me(appArgs)
        }
    }

    mainClassName = "io.advantageous.qbit.vertx.http.PerfServerTest"

    dependencies {
        compile project(':qbit-vertx')

        compile project(':qbit-boon')
        compile "org.slf4j:slf4j-api:[1.7,1.8)"
        compile 'ch.qos.logback:logback-classic:1.1.2'

        testCompile group: 'junit', name: 'junit', version: '4.10'
    }

}

project(':qbit-admin') {

    apply plugin: 'application'

    dependencies {
        compile project(':qbit-boon')
        compile project(':qbit-core')
        compile project(':qbit-vertx')
        compile "org.slf4j:slf4j-api:[1.7,1.8)"
        compile 'ch.qos.logback:logback-classic:1.1.2'

        testCompile group: 'junit', name: 'junit', version: '4.10'
        testCompile project(':qbit-test')

    }

    buildDockerfile {
        expose 8080
    }
}


project(':qbit-sample-server') {

    apply plugin: 'application'

    sourceSets.main.resources.srcDir 'src/main/java'

    mainClassName = "io.advantageous.qbit.sample.server.TodoServerMain"

    dependencies {
        compile project(':qbit-boon')
        compile project(':qbit-sample-model')
        compile project(':qbit-vertx')
        compile "org.slf4j:slf4j-api:[1.7,1.8)"
        compile 'ch.qos.logback:logback-classic:1.1.2'

        testCompile group: 'junit', name: 'junit', version: '4.10'
    }

    buildDockerfile {
        expose 8080
    }
}

project(':qbit-sample-client') {
    sourceSets.main.resources.srcDir 'src/main/java'
    apply plugin: 'application'

    mainClassName = "io.advantageous.qbit.sample.server.TodoClientMain"

    dependencies {
        compile project(':qbit-boon')
        compile project(':qbit-sample-model')
        compile "org.slf4j:slf4j-api:[1.7,1.8)"
        compile 'ch.qos.logback:logback-classic:1.1.2'

        testCompile group: 'junit', name: 'junit', version: '4.10'
    }

    buildDockerfile {
        expose 7070
    }
}


project(':qbit-sample-model') {
    sourceSets.main.resources.srcDir 'src/main/java'
    dependencies {
        compile project(':qbit-boon')
        compile project(':qbit-vertx')
        compile "org.slf4j:slf4j-api:[1.7,1.8)"
        compile 'ch.qos.logback:logback-classic:1.1.2'
        testCompile group: 'junit', name: 'junit', version: '4.10'
    }
}

class Dockerfile extends DefaultTask {
    def dockerfileInfo = ""
    def dockerDir = "$project.buildDir/docker"
    def dockerfileDestination = "$project.buildDir/docker/Dockerfile"
    def filesToCopy = []

    File getDockerfileDestination() {
        project.file(dockerfileDestination)
    }

    def from(image = "java") {
        dockerfileInfo += "FROM $image\r\n"
    }

    def maintainer(contact) {
        maintainer += "MAINTAINER $contact\r\n"
    }

    def add(sourceLocation, targetLocation) {
        filesToCopy << sourceLocation
        def file = project.file(sourceLocation)
        dockerfileInfo += "ADD $file.name ${targetLocation}\r\n"
    }

    def run(command) {
        dockerfileInfo += "RUN $command\r\n"
    }

    def env(var, value) {
        dockerfileInfo += "ENV $var $value\r\n"
    }

    def expose(port) {
        dockerfileInfo += "EXPOSE $port\r\n"
    }

    def workdir(dir) {
        dockerfileInfo += "WORKDIR $dir\r\n"
    }

    def cmd(command) {
        dockerfileInfo += "CMD $command\r\n"
    }

    def entrypoint(command) {
        dockerfileInfo += "ENTRYPOINT $command\r\n"
    }

    @TaskAction
    def writeDockerfile() {
        for (fileName in filesToCopy) {
            def source = project.file(fileName)
            def target = project.file("$dockerDir/$source.name")
            target.parentFile.mkdirs()
            target << source.bytes
        }
        def file = getDockerfileDestination()
        file.parentFile.mkdirs()
        file.write dockerfileInfo
    }
}

